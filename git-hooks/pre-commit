#!/usr/bin/perl -w

# File change counts
my $debian_files = 0;
my $ubuntu_files = 0;
my $others_files = 0;

# Config changes
my $i386_config = 0;
my $amd64_config = 0;
my $flavour_config = 0;

# Duh
$exit_val = 0;

# XXX --cached
open(FILES, "git-diff-index -M --name-only HEAD -- |") or die "ERROR: Calling git-diff-index";

while (<FILES>) {
	chomp();

	if (m|^debian/|) {
		$debian_files++;
		if (m/^debian\/config\/([^\/]*)\/config$/) {
			$i386_config++ if "$1" eq "i386";
			$amd64_config++ if "$1" eq "amd64";
		} elsif (m/^debian\/config\/.*\/config\..*/) {
			$flavour_config++;
		}
	} elsif (m|^ubuntu/|) {
		$ubuntu_files++;
	} else {
		$others_files++;
	}
}

close(FILES);

sub plog_err($$) {
	my $msg = $_[0];
	my $tok = $_[1];
	my $val = `git-config --bool ubuntu.$tok`;
	chomp($val);

	if ($val eq "true") {
		print STDERR "W: Override enforced by ubuntu.$tok\n";
	} else {
		print STDERR "E: $msg\n";
		print STDERR "   To override: git-config --bool ubuntu.$tok true\n";

		$exit_val = 1;
	}
	print STDERR "\n";
}

# We don't like commits that cross our subdirectories and the main kernel
# tree (it makes it hard to handle rebase and the like). So strongly
# suggest we don't do it
if ($others_files and ($debian_files or $ubuntu_files)) {
	plog_err("Cannot commit changes to debian/* or ubuntu/* with other file changes",
		 "others-changes-ok");
}

# Updating the common config file one arch and not the other could
# potentially cause a desync on the two (driver or option enabled in one,
# but disabled in another).
#
# TODO: We should really have a more thorough check on the diffs to these
# files, to do a line-by-line comparison.
if ($amd64_config != $i386_config) {
	plog_err("Changes to only one of i386 or amd64, possible desync of config options",
		 "config-desync-ok");
}

# Per flavour config changes are rare, and by nature, usually not correct.
# Suggest folks don't do it, and force them to think about it when they
# need to do it anyway.
if ($flavour_config) {
	plog_err("Changes to flavour specific config files are not allowed by default",
		 "flavour-changes-ok");
}

exit($exit_val);
